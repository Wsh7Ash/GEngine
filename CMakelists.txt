cmake_minimum_required(VERSION 3.20)

include(FetchContent)
FetchContent_Declare(
    imgui
    GIT_REPOSITORY https://github.com/ocornut/imgui.git
    GIT_TAG        docking
)
FetchContent_Declare(
    imguizmo
    GIT_REPOSITORY https://github.com/CedricGuillemet/ImGuizmo.git
    GIT_TAG        master
)
FetchContent_Declare(
    json
    URL https://github.com/nlohmann/json/releases/download/v3.11.3/json.tar.xz
)
FetchContent_MakeAvailable(imgui json imguizmo)

project(GameEngine
    VERSION     0.1.0
    LANGUAGES   CXX
    DESCRIPTION "GEngine — Phase 1 Foundation Layer"
)

# ── C++ standard ────────────────────────────────────────────────
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

# ── Output directories ──────────────────────────────────────────
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)

# ── Dependencies ───────────────────────────────────────────────
set(GLFW_BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
set(GLFW_BUILD_TESTS OFF CACHE BOOL "" FORCE)
set(GLFW_BUILD_DOCS OFF CACHE BOOL "" FORCE)
set(GLFW_INSTALL OFF CACHE BOOL "" FORCE)

add_subdirectory(deps/glfw)

# ── Compiler warnings ──────────────────────────────────────────
if(MSVC)
    add_compile_options(/W4 /WX- /permissive-)
    add_definitions(-D_CRT_SECURE_NO_WARNINGS)
else()
    add_compile_options(-Wall -Wextra -Wpedantic)
endif()


# ================================================================
#  Core Engine Library  (static)
# ================================================================

set(GE_CORE_HEADERS
    src/core/ge_core.h
    # Memory
    src/core/memory/allocator.h
    # Math
    src/core/math/mathUtils.h
    src/core/math/VecTypes.h
    src/core/math/Mat4x4.h
    src/core/math/quaternion.h
    # Debug
    src/core/debug/log.h
    src/core/debug/assert.h
    # Containers
    src/core/containers/dynamic_array.h
    src/core/containers/hash_map.h
    src/core/containers/handle.h
    # Platform
    src/core/platform/platform.h
    src/core/platform/Window.h
    src/core/platform/Input.h
    src/core/platform/GLFWInput.h
    src/core/platform/ImGuiLayer.h
    #ECS
    src/core/ecs/World.h
    src/core/ecs/System.h
    src/core/ecs/SystemManager.h
    src/core/ecs/Entity.h
    src/core/ecs/ComponentArray.h
    src/core/ecs/ComponentRegistry.h
    src/core/ecs/EntityManager.h
    src/core/ecs/Query.h
    src/core/ecs/components/TransformComponent.h
    src/core/ecs/components/VelocityComponent.h
    src/core/ecs/components/TagComponent.h
    src/core/ecs/components/MeshComponent.h
    src/core/ecs/components/SpriteComponent.h
    src/core/ecs/components/NativeScriptComponent.h
    src/core/ecs/ScriptableEntity.h
    src/core/ecs/systems/RenderSystem.h
    src/core/ecs/systems/ScriptSystem.h
    # Renderer
    src/core/renderer/RendererAPI.h
    src/core/renderer/GraphicsContext.h
    src/core/renderer/Shader.h
    src/core/renderer/Mesh.h
    src/core/renderer/Texture.h
    src/core/renderer/Framebuffer.h
    src/core/renderer/OrthographicCamera.h
    src/core/renderer/Renderer2D.h
    src/core/renderer/opengl/OpenGLContext.h
    src/core/renderer/opengl/OpenGLShader.h
    src/core/renderer/opengl/OpenGLMesh.h
    src/core/renderer/opengl/OpenGLTexture.h
    src/core/renderer/opengl/OpenGLFramebuffer.h
    src/core/renderer/dx11/DX11Context.h
    src/core/renderer/dx11/DX11Texture.h
    src/core/editor/EditorToolbar.h
    src/core/editor/SceneHierarchyPanel.h
    src/core/editor/ContentBrowserPanel.h
    src/core/editor/ViewportPanel.h
    src/core/scene/SceneSerializer.h
)

set(GE_CORE_SOURCES
    src/core/memory/allocator.cpp
    src/core/debug/log.cpp
    src/core/platform/platform.cpp
    src/core/platform/Window.cpp
    src/core/platform/GLFWInput.cpp
    src/core/platform/ImGuiLayer.cpp
    src/core/editor/EditorToolbar.cpp
    src/core/editor/SceneHierarchyPanel.cpp
    src/core/editor/ContentBrowserPanel.cpp
    src/core/editor/ViewportPanel.cpp
    src/core/scene/SceneSerializer.cpp
    src/core/renderer/RendererAPI.cpp
    src/core/renderer/Shader.cpp
    src/core/renderer/Mesh.cpp
    src/core/renderer/Texture.cpp
    src/core/renderer/Framebuffer.cpp
    src/core/renderer/OrthographicCamera.cpp
    src/core/renderer/Renderer2D.cpp
    src/core/renderer/opengl/OpenGLContext.cpp
    src/core/renderer/opengl/OpenGLShader.cpp
    src/core/renderer/opengl/OpenGLMesh.cpp
    src/core/renderer/opengl/OpenGLTexture.cpp
    src/core/renderer/opengl/OpenGLFramebuffer.cpp
    src/core/renderer/opengl/stb_image_impl.cpp
    src/core/renderer/dx11/DX11Context.cpp
    src/core/renderer/dx11/DX11Texture.cpp
    src/core/ecs/systems/RenderSystem.cpp
    src/core/ecs/systems/ScriptSystem.cpp
    deps/glad/src/glad.c
    
    # ImGui
    ${imgui_SOURCE_DIR}/imgui.cpp
    ${imgui_SOURCE_DIR}/imgui_draw.cpp
    ${imgui_SOURCE_DIR}/imgui_tables.cpp
    ${imgui_SOURCE_DIR}/imgui_widgets.cpp
    ${imgui_SOURCE_DIR}/imgui_demo.cpp
    ${imgui_SOURCE_DIR}/backends/imgui_impl_glfw.cpp
    ${imgui_SOURCE_DIR}/backends/imgui_impl_opengl3.cpp
    
    # ImGuizmo
    ${imguizmo_SOURCE_DIR}/ImGuizmo.cpp
)

add_library(ge_core STATIC ${GE_CORE_SOURCES} ${GE_CORE_HEADERS})

target_include_directories(ge_core
    PUBLIC
        ${CMAKE_SOURCE_DIR}/src/core
        ${CMAKE_SOURCE_DIR}/deps/glfw/include
        ${CMAKE_SOURCE_DIR}/deps/glad/include
        ${CMAKE_SOURCE_DIR}/src/core/editor
        ${imgui_SOURCE_DIR}
        ${imgui_SOURCE_DIR}/backends
        ${imguizmo_SOURCE_DIR}
        ${json_SOURCE_DIR}/include
)

# Platform-specific linking
if(WIN32)
    target_compile_definitions(ge_core PUBLIC GE_PLATFORM_WINDOWS=1)
elseif(UNIX AND NOT APPLE)
    target_compile_definitions(ge_core PUBLIC GE_PLATFORM_LINUX=1)
elseif(APPLE)
    target_compile_definitions(ge_core PUBLIC GE_PLATFORM_MACOS=1)
endif()


# ================================================================
#  Main executable
# ================================================================

add_executable(GameEngine main.cpp)
target_link_libraries(GameEngine PRIVATE ge_core glfw nlohmann_json::nlohmann_json)


# ================================================================
#  Unit tests  (optional — build with -DBUILD_TESTS=ON)
# ================================================================

option(BUILD_TESTS "Build unit tests" OFF)

if(BUILD_TESTS)
    enable_testing()

    add_executable(ge_tests
        tests/test.cpp
    )
    target_link_libraries(ge_tests PRIVATE ge_core)
    add_test(NAME CoreTests COMMAND ge_tests)

    add_executable(ge_ecs_tests
        tests/test_ecs.cpp
    )
    target_link_libraries(ge_ecs_tests PRIVATE ge_core)
    add_test(NAME EcsTests COMMAND ge_ecs_tests)
endif()
